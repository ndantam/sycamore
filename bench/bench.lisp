;;;; -*- Lisp -*-
;;;;
;;;; Copyright (c) 2012, Georgia Tech Research Corporation
;;;; Copyright (c) 2025, Colorado School of Mines
;;;; All rights reserved.
;;;;
;;;; Author(s): Neil T. Dantam <ntd@gatech.edu>
;;;; Georgia Tech Humanoid Robotics Lab
;;;; Under Direction of Prof. Mike Stilman
;;;;
;;;; This file is provided under the following "BSD-style" License:
;;;;
;;;;   Redistribution and use in source and binary forms, with or
;;;;   without modification, are permitted provided that the following
;;;;   conditions are met:
;;;;   * Redistributions of source code must retain the above
;;;;     copyright notice, this list of conditions and the following
;;;;     disclaimer.
;;;;   * Redistributions in binary form must reproduce the above
;;;;     copyright notice, this list of conditions and the following
;;;;     disclaimer in the documentation and/or other materials
;;;;     provided with the distribution.
;;;;
;;;;   THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND
;;;;   CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES,
;;;;   INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
;;;;   MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
;;;;   DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR
;;;;   CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
;;;;   SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
;;;;   NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
;;;;   LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
;;;;   HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;;;;   CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
;;;;   OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE,
;;;;   EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.


(in-package :sycamore)

(ql:quickload :fset)

(ql:quickload :cl-hamt)

(defparameter *bench-data-file-1*
  (make-pathname :directory '(:absolute "tmp") :name "sycamore-bench-1" :type "dat"))

(defparameter *bench-data-file-2*
  (make-pathname :directory '(:absolute "tmp") :name "sycamore-bench-2" :type "dat"))

(defun bench-generate-data (&key
                            (output-1 *bench-data-file-1*)
                            (output-2 *bench-data-file-2*)
                            (count-1 (expt 2 21))
                            (max-1 (* 2 count-1))
                            (count-2 count-1)
                            (max-2 max-1))
  (flet ((emit (count max output)
           (with-open-file (s output :direction :output :if-exists :supersede :if-does-not-exist :create)
             (format s "宁祜镳骘忮祜泔躅泔祆邈蜥钿镯磲┅┅┅ㄥ黹泔躅舡磲秕麴豸暴ㄥ黹泔躅舡磲秕麴豸博┅ㄤ彐躅忮钽璀祜徜疳翳钺礤鏖翳镳孱骈戾疳翳钺礤轰轵邈糸镱洪铕豸祜镳骘蝈徜铋铋飑麒殪泔祆邈椹┅ㄤ彐躅糸礤珏铄蜥ㄢ蹰熹脲扉篝ㄢ孱汨祜徜忮钽璀溽翎骈戾豹┅扉篝ㄢ孱汨祜徜忮钽璀溽翎骈戾勃┅轭箦螋蝈盹鲥躅轱轭翦蝮邈糸镱轭翦蝮邈糸镱溟骀弪孱沐篚怏弭骈钿秕麴豸篝犷溽蜾秕麴豸钺礤＋筲沆筲屮艉玢烘蹯舂戾è篝犷溽蜾秕麴豸秕麴豸镡戥暴镡戥博灬忮祗è痱瀛翦篝翦篝钺礤ㄦ矧磲秕麴豸ズ梁孩钺礤翦篝钺礤＋筲沆筲屮艉玢┅ㄩ钺礤ㄦ矧磲洛钽桧狎塍义篚祠骘孩钺礤ㄦ矧磲洛钽桧狎塍义篚祠孩┅换怩殪痱瀛翦篝⑩蹰熹镡赍泗雹箦赳镡戥糸礤ㄦ躅汜祆怩殪扉篝暴┅痱瀛翦篝⑩蹰熹镡赍泗并箦赳镡戥糸礤ㄦ躅汜祆怩殪扉篝博┅换骈钿麒孱骈钿痱瀛翦篝㈡轭轭雹糸礤祜镳骘轭扉篝滹ㄦ躅汜祆骈钿镡戥┅痱瀛翦篝㈡轭轭雹糸礤祜镳骘轭扉篝滹ㄦ躅汜祆骈钿镡戥┅痱瀛翦篝㈡轭轭并糸礤祜镳骘轭扉篝滹ㄦ躅汜祆骈钿镡戥┅┅换轭箦螋麒孱轭箦螋痱瀛翦篝㈤铙弪轭麸雹糸礤祜镳骘轭扉篝骘ㄦ躅汜祆轭箦螋镡戥翳孱ㄦ躅汜祆轭箦螋┅痱瀛翦篝㈤铙弪轭麸并糸礤祜镳骘轭扉篝骘ㄦ躅汜祆轭箦螋镡戥翳孱ㄦ躅汜祆轭箦螋┅┅换蝈盹鲥麒孱蝈盹鲥痱瀛翦篝Ⅱ屙秭骝镯雹糸礤祜镳骘轭扉篝骘ㄦ躅汜祆蝈盹鲥镡戥翳孱ㄦ躅汜祆轭箦螋┅痱瀛翦篝Ⅱ屙秭骝镯并糸礤祜镳骘轭扉篝骘ㄦ躅汜祆蝈盹鲥镡戥翳孱ㄦ躅汜祆轭箦螋┅┅换躅轱麒孱躅轱痱瀛翦篝Ⅴ铋镱并糸礤ㄦ躅汜祆躅轱镡戥镡戥博痱瀛翦篝Ⅴ铋镱雹糸礤ㄦ躅汜祆躅轱镡戥镡戥暴┅换轭翦蝮邈糸镱麒孱轭翦蝮邈糸镱痱瀛翦篝㈤铘弪箦泗轱并糸礤ㄦ躅汜祆轭翦蝮邈糸镱镡戥镡戥博痱瀛翦篝㈤铘弪箦泗轱雹糸礤ㄦ躅汜祆轭翦蝮邈糸镱镡戥镡戥暴┅换轭翦蝮邈糸镱麒孱轭翦蝮邈糸镱痱瀛翦篝㈤铘弪箦泗轱铕并糸礤ㄦ躅汜祆轭翦蝮邈糸镱镡戥镡戥博痱瀛翦篝㈤铘弪箦泗轱铕雹糸礤ㄦ躅汜祆轭翦蝮邈糸镱镡戥镡戥暴┅换篚怏弭麒孱篚怏弭痱瀛翦篝Ⅲ踱箦并糸礤ㄦ躅汜祆篚怏弭镡戥镡戥博痱瀛翦篝Ⅲ踱箦雹糸礤ㄦ躅汜祆篚怏弭镡戥镡戥暴┅换溟骀弪孱沐麒孱溟骀弪孱沐痱瀛翦篝溟骀弪孱沐并糸礤ㄦ躅汜祆溟骀弪孱沐镡戥镡戥博痱瀛翦篝溟骀弪孱沐雹糸礤ㄦ躅汜祆溟骀弪孱沐镡戥镡戥暴┅┅铋飑ㄤ彐躅糸礤麾ī换怩殪戾è泔眇狎灬礅溽ㄡ猢ㄤ邈灬蝈豉疱骈铛猢ō猢┅糸礤珏铄蜥灬礅溽ㄡㄢ蹰熹麾趄邋泔眇狎铋岍洪铙弪灬礅溽镡麾趄邋轭箦螋镡泔眇狎濠候屙秭灬礅溽镡麾趄邋蝈盹鲥镡泔眇狎濠烘轭灬礅溽镡ㄢ轭狎趄邋骈钿镡泔眇狎濠乎铋镱灬礅溽麾趄邋躅轱泔眇狎濠洪铘弪箦泗轱灬礅溽麾趄邋轭翦蝮邈糸镱泔眇狎濠洪铘弪箦泗轱铕灬礅溽麾趄邋轭翦蝮邈糸镱泔眇狎濠轰殒驽蝈钽灬礅溽麾趄邋溟骀弪孱沐泔眇狎濠后踱箦灬礅溽麾趄邋篚怏弭泔眇狎濠侯犴⒂倜镣弦藕茁┅ㄤ彐躅糸礤骟弭ī糸礤珏铄蜥灬礅溽ㄡㄦ镬＇骟弭瑚轸ㄦ箦艉屙痿箦舂岍洪铙弪灬礅溽镡ㄦ箦艉鏖翳镡┅烘轭灬礅溽镡ㄦ箦艉礤礅弪镡戛候屙秭灬礅溽镡ㄦ箦艉戾篌镡┅乎铋镱＇骟弭乎铋镱洪铘弪箦泗轱＇骟弭洪铘弪箦泗轱后踱箦＇骟弭后踱箦艨轰殒驽蝈钽＇骟弭后弭溟骀弪孱沐侯犴⑵优寓┅ㄤ彐躅糸礤栳眙ī糸礤珏铄蜥灬礅溽ㄡ扉篝栳箬箦岍洪铙弪＇栳箬箦舡轭箦螋烘轭＇栳箬箦舡骈钿乎铋镱＇栳箬箦舡躅轱洪铘弪箦泗轱＇栳箬箦舡轭翦蝮邈糸镱洪铘弪箦泗轱铕＇栳箬箦舡轭翦蝮邈糸镱轰殒驽蝈钽＇栳箬箦舡溟骀弪孱沐后踱箦＇栳箬箦舡篚怏弭侯犴⒂倜镣弦藕攘驮┅ㄤ彐躅糸礤沆栳眙ī糸礤珏铄蜥灬礅溽ㄡ蝈漉沐＇沆栳眙后弭轭箦螋洪铋糸犰鲠祯ㄣ飙栳眙哄眇豉箦舂┅洪铙弪灬礅溽镡ㄣ飙栳眙后弭轭箦螋镡┅烘轭＇沆栳眙后弭祜镫躔侯犴⒚汰攘驮┅ㄤ彐躅糸礤栳箬翎忪ī糸礤珏铄蜥灬礅溽ㄡ戾è栳箬磲脲栳箬翎忪濠┅ㄤ镬轶岍箦翩ㄧ弭栳箬栳箬舂栳箬┅缓轭箦螋灬礅溽镡ㄣ飙栳眙后弭轭箦螋镡┅烘轭灬礅溽ㄨ狍ㄧ弭栳箬栳箬┅侯犴⑷劣拳粤绿泞┅ㄤ彐躅糸礤犰é脲泔躅磲泔躅舂麒孱ㄡ钿泔躅磲ㄢ孱汨珏铄蜥翦溽翎恒秕铘泔躅喉狲磲┅糸礤麾糸礤骟弭铋飑换换换换换换换换换换换换换换换换换换洛钽桧狎葬忪清铄蜥糸镱换换换换换换换换换换换换换换换换换换ㄤ彐躅珏舡怡翦蟓泔铙邃ī＋筲沆筲屮艉珏舡怡翦蟓泔铙邃－筲沆癌ㄤ彐躅趄獒翳躅轸弪狒轱铙钺礤镳溟蝈泗矧ㄤ邈灬蝈豉疱骢钽糸镱翳躅氅＋筲沆筲屮艉玢烘蹯舂戾è舭ㄧ弭轭翦蝾犰蝓瞽糸礤┅ㄢㄧ弭怡翦蟓泔铙邃┅痱镧ㄦ躅汜祆翳躅氅戾舄è舯ㄧ弭轭翦蝾犰蝓瞽糸礤┅ㄢㄧ弭怡翦蟓泔铙邃┅趑ǒō舯舭í轸弪狒轱铙轭翦蝾犰糸礤躅轸蟓疱颦箦泔钿┅ㄢǒō獗獍轸弪狒轱铙┅戾è溟蝈泗矧ㄦ矧磲铋怜怜溟蝈泗矧镳┅ㄥ铙躜瀛溟蝈泗矧殄蟓屮轶溟蝈泗矧鏖翳镳孱骈戾礤蜱瀛疳翳钺礤篝蜷铉钺礤溟蝈泗矧轰轵邈糸镱猴豸瘐洪姝滹弩铒舡屮轶恒蝈狒洪姝屮轶趔横痧孱洎痱轭扉篝侯呼轫ㄣ镥蜚趑т秕忪瀛骒镝舂衡翦ㄣ镥蜚忖т秕忪瀛骒镝舂螬┅┅┅ㄤ彐疳蜥礤翦痨轶趔扉篝换攘驮扉篝侯犴簌汜盹蝈栳眙恒镱篝蝓泗＇扉篝栳箬箦洪铙弪＇栳箬箦舡轭箦螋候屙秭＇栳箬箦舡蝈盹鲥烘轭＇栳箬箦舡骈钿乎铋镱＇栳箬箦舡躅轱洪铘弪箦泗轱＇栳箬箦舡轭翦蝮邈糸镱轰殒驽蝈钽＇栳箬箦舡溟骀弪孱沐换茁戾è泔眇狎灬礅溽ㄡ猢ㄤ邈灬蝈豉疱骈铛猢ō猢┅扉篝侯犴簌汜盹蝈麾趄邋恒镱篝蝓泗灬礅溽ㄡㄦ镬＇趄邋箦舡轭箦螋磲脲趄邋箦泔眇狎濠岍洪铙弪＇趄邋箦舡轭箦螋候屙秭＇趄邋箦舡蝈盹鲥烘轭＇趄邋箦舡骈钿乎铋镱＇趄邋箦舡躅轱洪铘弪箦泗轱＇趄邋箦舡轭翦蝮邈糸镱轰殒驽蝈钽＇趄邋箦舡溟骀弪孱沐┅换廖由攘尤扉篝侯犴Я斡森攘尤粤绿恒镱篝蝓泗灬礅溽ㄡ戾è栳箬磲脲栳箬翎忪濠┅ㄤ镬轶岍箦翩ㄧ弭栳箬栳箬舂栳箬┅烘轭灬礅溽ㄨ狍ㄧ弭栳箬栳箬┅换朴旁扉篝侯犴ф箦恒镱篝蝓泗灬礅溽ㄡㄦ镬＇骟弭瑚轸ㄦ箦艉屙痿箦舂岍洪铙弪灬礅溽镡ㄦ箦艉鏖翳镡┅烘轭灬礅溽镡ㄦ箦艉礤礅弪镡戛候屙秭灬礅溽镡ㄦ箦艉戾篌镡┅乎铋镱＇骟弭乎铋镱洪铘弪箦泗轱＇骟弭洪铘弪箦泗轱轰殒驽蝈钽＇骟弭后弭溟骀弪孱沐博换锰攘驮扉篝侯犴с飙栳眙恒镱篝蝓泗灬礅溽ㄡㄦ镬＇沆栳眙后弭轭箦螋ㄣ飙栳眙哄眇豉箦舂岍洪铙弪＇沆栳眙后弭轭箦螋烘轭＇沆栳眙后弭祜镫躔候屙秭＇沆栳眙后弭蝈盹鲥┅ㄤ彐磲泸趄獒飙翳躅ㄩ翦蜥糸镱怙澌怙澌鏖翳珏铙眢轸颟啜戾è轸轸弪狒轱铙┅ㄩ轸暴灬礅溽ī棱镤灬礅溽īㄤ雉轫弩ìū轸颟棱镤棱镤┅┅ㄤ彐磲泸麒孱翳躅è鲠簌礅镬痨轶舂怙澌怙澌啜戾è鲠ㄧ弭痨轶簌礅镬┅ㄦ矧磲立簌礅镬麒孱鲠戾è鲠翳骢钽糸镱鲠颟┅棱镤┅┅ㄤ彐躅筢眇戾箦痨轶轸弪狒轱铙扉篝扉篝溟蝈泗矧ㄦ矧磲ι托毯连谓默蠼蘑ㄧ弭痨轶侯犴濠氅戾舄è钺礤ㄧ弭痨轶侯犴濠ㄣ镱篝蝓泗翳骢钽糸镱ㄧ弭痨轶恒镱篝蝓泗┅箦舡趄獒趄獒飙翳躅轸弪狒轱铙ㄦ躅汜祆泔铙趄蹉扉篝暴轸弪狒轱铙钺礤泔铙趄蹉簪溟蝈泗矧┅箦舡趄獒趄獒飙翳躅轸弪狒轱铙ㄦ躅汜祆泔铙趄蹉扉篝博轸弪狒轱铙钺礤泔铙趄蹉簪溟蝈泗矧┅换深箦螋麒孱翳躅ㄦ洪铙弪痨轶舂趄獒趄獒飙翳躅轸弪狒轱铙ㄤ镬轶ㄥ祠扉篝博ㄦ躅汜祆箦舡屐舂┅í轸弪狒轱铙钺礤㈤铙弪簪溟蝈泗矧趄獒趄獒飙翳躅轸弪狒轱铙ㄤ镬轶ㄥ祠扉篝暴ㄦ躅汜祆箦舡屐舂┅í轸弪狒轱铙钺礤㈤铙弪簪溟蝈泗矧┅换崎钿麒孱翳躅ㄦ烘轭痨轶舂趄獒趄獒飙翳躅轸弪狒轱铙ㄤ镬轶ㄥ祠扉篝博ㄦ躅汜祆箦舡屐舂┅í轸弪狒轱铙钺礤㈡轭洧溟蝈泗矧趄獒趄獒飙翳躅轸弪狒轱铙ㄤ镬轶ㄥ祠扉篝暴ㄦ躅汜祆箦舡屐舂┅í轸弪狒轱铙钺礤㈡轭洧溟蝈泗矧┅换义盹鲥麒孱翳躅ㄦ候屙秭痨轶舂趄獒趄獒飙翳躅轸弪狒轱铙ㄤ镬轶ㄥ祠扉篝博ㄦ躅汜祆箦舡屐舂┅í轸弪狒轱铙钺礤Ⅱ屙秭澧溟蝈泗矧趄獒趄獒飙翳躅轸弪狒轱铙ㄤ镬轶ㄥ祠扉篝暴ㄦ躅汜祆箦舡屐舂┅í轸弪狒轱铙钺礤Ⅱ屙秭澧溟蝈泗矧┅换疹轱麒孱翳躅ㄦ乎铋镱痨轶舂趄獒趄獒飙翳躅轸弪狒轱铙ㄦ躅汜祆箦舡箦舡博轸弪狒轱铙钺礤Ⅴ铋镱溟蝈泗矧┅换深翦蝮邈糸镱麒孱翳躅ㄦ洪铘弪箦泗轱痨轶舂趄獒趄獒飙翳躅轸弪狒轱铙ㄦ躅汜祆箦舡箦舡博轸弪狒轱铙钺礤㈤铘弪箦泗轱睥溟蝈泗矧┅换拈骀弪孱沐麒孱翳躅ㄦ轰殒驽蝈钽痨轶舂趄獒趄獒飙翳躅轸弪狒轱铙ㄦ躅汜祆箦舡箦舡博轸弪狒轱铙钺礤溟骀弪孱沐溟蝈泗矧┅┅ㄤ彐躅忮钽璀箝痫麇颟戾èㄥ痿痫麇颟┅ㄣ镱磲ㄦ祜矧ǒㄥ痿卑瞟┅┅ㄤ彐疳蜥礤翦忮钽璀箝弩祜镳骘骝镯躔麸舶泔祆邈ㄢ孱汨箝椹┅ㄤ彐疳蜥礤翦忮钽璀筢眇戾螵嘲ㄤ彐躅珏瞽扉篝箝濠戾è磲脲栳箬翎忪后辁箝濠磲í箝濠┅祜镳躅糸ㄨ狍璀翎忪瀛泔躅瑭箝濠骘蜥钿镯磲滹箦翩ㄧ弭栳箬瑭┅ㄨ狍璀翎忪瀛鲠祯弩瑭┅ㄤ彐躅蝓瞽忮钽é镳糸镱犰ㄤ轵邈麸蝙繇鸠簌汜盹蝈忮钽璇┅换描邈拈蝈泗矧麒孱痱镡瀛骈戾溟蝈泗矧ㄥ蝌矧⒛轵邈麸蝙帼璃犰蝈徜屮轶趔蝈骢箝铉麸秭弪黩轸瀹溟蝈泗矧┅ㄥ铙躜瀛溟蝈泗矧殄蟓屮轶溟蝈泗矧换阴翦篝祜镳骘轸弪狒轱铙轭忮钽璀箝弩滹祜镳骘忮祜忮钽璀筢眇戾螵骘扉篝ㄧ孱扉篝瞟骘扉篝ㄧ孱扉篝瞟滹祜镳骘痨轶轭痨轶趔滹筢眇戾箦痨轶轸弪狒轱铙扉篝扉篝溟蝈泗矧┅┅ㄤ彐躅忮钽璀扉铄疳翳钺礤戾è璀糸礤磲脲栳箬翎忪濠ㄨ怡翦磲脲栳箬翎忪濠┅换义徜溽翎鏖翳镳孱骈戾疳翳钺礤轰轵邈糸镱洪铕豸祜镳骘扉铄蝈徜铋铋飑麒殪扉铄滹ㄤ弩趄蹉趱蜷铉忾钿é脲糸礤怡翦螬扉铄ㄡ篌弪ㄡ钿糸礤怡翦螬瘐箬糸礤ㄧ弭栳箬璀糸礤┅瘐箬怡翦ㄧ弭栳箬璀怡翦螬┅┅换郁狒弩祜镳骘脲轭箫螋ㄣ镳扉篝ㄨ狍璀翎忪瀛脲璀糸礤┅＇冀骘糸礤扉篝ㄧ弭栳箬脲璀糸礤泔祆邈礤犷糸礤扉篝┅┅ㄤ彐躅忮钽璀桢徜弪é镳糸镱犰篝蝈犴舂戾è扉篝ㄣ镱⑸眇戾礤铘狒轱睥箫螋磲ъ轶＇汜忮钽璀箝弩＇冀┅┅ㄦ矧磲篝蝈犴|%" list)
    (dotimes (i (length list))
      (format stream "|----"))
    (format stream "|")))

(defun bench-section (heading &optional (stream t))
  (format stream "&A%A%%"
          heading
          (make-string (length heading) :initial-element #\=)))

(defun bench-subsection (heading &optional (stream t))
  (format stream "&A%A"
          heading
          (make-string (length heading) :initial-element #\-)))

(defun bench-line-0 (name line &optional (stream t))
  (format stream "&| A |" name)
  (loop for mean in line
        for (factor . unit) = (cond ((< mean 1e-3)
                                     (cons 1e6 "us"))
                                    ((< mean 1)
                                     (cons 1e3 "ms"))
                                    (t (cons 1 "s")))
        do (format stream " ,2F A |"
                   (* factor mean)
                   unit)))


(defun bench-line-rel (name line-0 line &optional (stream t))
  (format stream "&| A |" name)
  (loop for mean-0 in line-0
        for mean in line
        do (format stream " ,2Fx |"
                   (/ mean mean-0))))

(defun bench-table (directory &optional (stream t))
  (bench-section (string-upcase (car (last (pathname-directory directory))))
                 stream)
  (bench-header stream)
  (let* ((name-0 (string (getf (car *plists*) :name)))
         (line-0 (bench-line (concatenate 'string (namestring directory)
                                          name-0))))
    (bench-line-0 name-0 line-0 stream)
    (loop for pp in (cdr *plists*)
          for name = (string (getf pp :name))
          for pathname = (concatenate 'string (namestring directory) name)
          when (probe-file pathname)
            do (bench-line-rel name line-0 (bench-line pathname)
                               stream))))


(defun bench-cpu ()
  #-sbcl
  "Unknown"
  #+sbcl
  (with-output-to-string (s)
    (sb-ext:run-program
     "/bin/sh"
     '("-c"
       "cat /proc/cpuinfo | grep 'model name' | head -n 1 | sed -e 's/model name\\s*:\\s*//'")
     :output s)))
(defun bench-uname ()
  #-sbcl
  "Unknown"
  #+sbcl
  (with-output-to-string (s)
    (sb-ext:run-program
     "/bin/sh"
     '("-c"
       "uname -a"
       )
     :output s)))

(defun bench-text (&optional (directory "/tmp/sycamore-bench/") (stream t))

  (bench-section "Sycamore Persistent Set Benchmarks" stream)
  (format stream "&This document contains timing benchmarks of the persistent set implementations in Sycamore, FSET, and CL-HAMT, along with a baseline of (mutable) ANSI CL Hash-Tables. The performance ranking is as follows and is generally consistent across the tested operations:%%")
  (format stream "&1. ANSI CL Hash-Tables (fastest construction and find, but mutable only)")
  (format stream "&2. Sycamore HAMTs (fastest persistent set)")
  (format stream "&3. Sycamore WB-Trees")
  (format stream "&4. FSet")
  (format stream "&5. CL-HAMT")

  (format stream "&%")
  (bench-section "Test Setup" stream)

  (bench-subsection "Data" stream)
  (format stream "&- Operations performed for random sets of the stated sizes")
  (format stream "&- D samples (random sets) per size" *bench-samples*)
  (format stream "&- Results for A are shown as mean run time"
          (getf (car *plists*) :name))
  (format stream "&- Results for others are shown as relative run time (/ TIME-OTHER TIME-A), equivalent to the speedup of A over OTHER"
          (getf (car *plists*) :name) (getf (car *plists*) :name))

  (format stream "&%")
  (bench-subsection "System" stream)
  (multiple-value-bind
        (second minute hour day month year day-of-week dst-p tz)
      (get-decoded-time)
    (declare (ignore second day-of-week dst-p tz))
    (format stream "&- Date: D-2,'0D-2,'0D, 2,'0D:2,'0D"
            year month day minute hour))
  (format stream "&- Uname: A" (bench-uname))
  (format stream "&- CPU: A" (bench-cpu))
  (format stream "&- Lisp Implementation: A A"
          (lisp-implementation-type) (lisp-implementation-version))


  (loop for op in '(construct insert find remove union intersection difference)
        for subdir = (concatenate 'string (namestring directory)
                                  (string-downcase (string op))
                                  "/")
        do (format stream "%%")
           (bench-table subdir stream)))

(defun bench-file (filespec &optional (directory "/tmp/sycamore-bench/"))
  (with-open-file (stream filespec
                          :direction :output
                          :if-does-not-exist :create
                          :if-exists :supersede)
    (bench-text directory stream)))
